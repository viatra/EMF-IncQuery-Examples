package bodymodel.ymca

import "http://org/jnect/bodymodel"

uses bodymodel.MovingAverageCalculator;
uses bodymodel.ymca.VectorMaths;
uses bodymodel.ymca.YMCAConstants;


/*
 * This pattern is buggy! Depending on the relative position of (0,0,0) this may yield wrong results.
 */
@QueryExplorer(display = true)
pattern rightHandAboveHead(RH:RightHand,H:Head) {
	RightHand.y(RH,RHy);
	Head.y(H,Hy);
	check(
		MovingAverageCalculator::getCalculator("HY").addValue(Hy).movingAvg < 
		 MovingAverageCalculator::getCalculator("RHY").addValue(RHy).movingAvg
	);
}



@QueryExplorer(display = true)
pattern leftHandAboveHead(LH:LeftHand,H:Head) {
	LeftHand.y(LH,LHy);
	Head.y(H,Hy);
	check(
		MovingAverageCalculator::getCalculator("HY").addValue(Hy).movingAvg < 
		 MovingAverageCalculator::getCalculator("LHY").addValue(LHy).movingAvg
	);
}

/*
 * See https://github.com/ujhelyiz/EMF-IncQuery/issues/312
 */
/* @QueryExplorer(display = true)
pattern handsAreClose_buggy(LH:LeftHand,RH:RightHand) {
	LeftHand.x(LH,LHX);
	LeftHand.y(LH,LHY);
	LeftHand.z(LH,LHZ);
	RightHand.x(RH,RHX);
	RightHand.y(RH,RHY);
	RightHand.z(RH,RHZ);
	check({
		var float lhx = LHX
		var float lhy = LHY
		var float lhz = LHZ
		var float rhx = RHX
		var float rhy = RHY
		var float rhz = RHZ
		// I suspect the issue stems from the fact that EMF domain objects are wired into Java code
		VectorMaths::lengthOf(VectorMaths::createVector(LH,RH)) < YMCAConstants::closeDistance
		});
} */

@QueryExplorer(display = true)
pattern handsAreClose(LH:LeftHand,RH:RightHand) {
	LeftHand.x(LH,LHX);
	LeftHand.y(LH,LHY);
	LeftHand.z(LH,LHZ);
	RightHand.x(RH,RHX);
	RightHand.y(RH,RHY);
	RightHand.z(RH,RHZ);
	check( VectorMaths::lengthOf(
		VectorMaths::createVector(
			MovingAverageCalculator::getCalculator("LHX").addValue(LHX).movingAvg,
			MovingAverageCalculator::getCalculator("LHY").addValue(LHY).movingAvg,
			MovingAverageCalculator::getCalculator("LHZ").addValue(LHZ).movingAvg, 
			MovingAverageCalculator::getCalculator("RHX").addValue(RHX).movingAvg,
			MovingAverageCalculator::getCalculator("RHY").addValue(RHY).movingAvg,
			MovingAverageCalculator::getCalculator("RHZ").addValue(RHZ).movingAvg
		)) < YMCAConstants::closeDistance );
}

@QueryExplorer(display = true)
pattern handsAreApart(LH:LeftHand,RH:RightHand) {
	LeftHand.x(LH,LHX);
	LeftHand.y(LH,LHY);
	LeftHand.z(LH,LHZ);
	RightHand.x(RH,RHX);
	RightHand.y(RH,RHY);
	RightHand.z(RH,RHZ);
	check( VectorMaths::lengthOf(
		VectorMaths::createVector(
			MovingAverageCalculator::getCalculator("LHX").addValue(LHX).movingAvg,
			MovingAverageCalculator::getCalculator("LHY").addValue(LHY).movingAvg,
			MovingAverageCalculator::getCalculator("LHZ").addValue(LHZ).movingAvg, 
			MovingAverageCalculator::getCalculator("RHX").addValue(RHX).movingAvg,
			MovingAverageCalculator::getCalculator("RHY").addValue(RHY).movingAvg,
			MovingAverageCalculator::getCalculator("RHZ").addValue(RHZ).movingAvg
		)) > YMCAConstants::apartDistance );
}


@QueryExplorer(display = true)
pattern feetAreClose(LF:LeftFoot,RF:RightFoot) {
	LeftFoot.x(LF,LFX);
	LeftFoot.y(LF,LFY);
	LeftFoot.z(LF,LFZ);
	RightFoot.x(RF,RFX);
	RightFoot.y(RF,RFY);
	RightFoot.z(RF,RFZ);
	check( VectorMaths::lengthOf(
		VectorMaths::createVector(
			MovingAverageCalculator::getCalculator("LFX").addValue(LFX).movingAvg,
			MovingAverageCalculator::getCalculator("LFY").addValue(LFY).movingAvg,
			MovingAverageCalculator::getCalculator("LFZ").addValue(LFZ).movingAvg, 
			MovingAverageCalculator::getCalculator("RFX").addValue(RFX).movingAvg,
			MovingAverageCalculator::getCalculator("RFY").addValue(RFY).movingAvg,
			MovingAverageCalculator::getCalculator("RFZ").addValue(RFZ).movingAvg
		)) < YMCAConstants::closeDistance );
}

@QueryExplorer(display = true)
pattern feetAreApart(LF:LeftFoot,RF:RightFoot) {
	LeftFoot.x(LF,LFX);
	LeftFoot.y(LF,LFY);
	LeftFoot.z(LF,LFZ);
	RightFoot.x(RF,RFX);
	RightFoot.y(RF,RFY);
	RightFoot.z(RF,RFZ);
	check( VectorMaths::lengthOf(
		VectorMaths::createVector(
			MovingAverageCalculator::getCalculator("LFX").addValue(LFX).movingAvg,
			MovingAverageCalculator::getCalculator("LFY").addValue(LFY).movingAvg,
			MovingAverageCalculator::getCalculator("LFZ").addValue(LFZ).movingAvg, 
			MovingAverageCalculator::getCalculator("RFX").addValue(RFX).movingAvg,
			MovingAverageCalculator::getCalculator("RFY").addValue(RFY).movingAvg,
			MovingAverageCalculator::getCalculator("RFZ").addValue(RFZ).movingAvg
		)) > YMCAConstants::apartDistance );
}


/**
 * Right arm is stretched straight.
 */
@QueryExplorer(display = true)
pattern stretchedRightArm(RH:RightHand, // hand 
					 RE:RightElbow, // elbow
					 RS:RightShoulder // shoulder
) {
	// indicate coordinates to be used for check calculation
	PositionedElement.x(RH,RHX);
	PositionedElement.y(RH,RHY);
	PositionedElement.z(RH,RHZ);
	PositionedElement.x(RE,REX);
	PositionedElement.y(RE,REY);
	PositionedElement.z(RE,REZ);
	PositionedElement.x(RS,RSX);
	PositionedElement.y(RS,RSY);
	PositionedElement.z(RS,RSZ);
	// check that the angle between the lower arm (H-E)
	// and upper arm (S-E) is around PI (180')
	check({
		// we need to "wire" attribute values into the check expression
		var float angleBetween = VectorMaths::angleBetween(
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("RHX").addValue(RHX).movingAvg, 
				MovingAverageCalculator::getCalculator("RHY").addValue(RHY).movingAvg, 
				MovingAverageCalculator::getCalculator("RHZ").addValue(RHZ).movingAvg, 
				MovingAverageCalculator::getCalculator("REX").addValue(REX).movingAvg, 
				MovingAverageCalculator::getCalculator("REY").addValue(REY).movingAvg, 
				MovingAverageCalculator::getCalculator("REZ").addValue(REZ).movingAvg
			),
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("RSX").addValue(RSX).movingAvg, 
				MovingAverageCalculator::getCalculator("RSY").addValue(RSY).movingAvg, 
				MovingAverageCalculator::getCalculator("RSZ").addValue(RSZ).movingAvg, 
				MovingAverageCalculator::getCalculator("REX").addValue(REX).movingAvg, 
				MovingAverageCalculator::getCalculator("REY").addValue(REY).movingAvg, 
				MovingAverageCalculator::getCalculator("REZ").addValue(REZ).movingAvg
			)
		)
		// System::out.println(angleBetween)
		(Math::PI - angleBetween) < YMCAConstants::angleTolerance
	});
}

/**
 * Right arm is bent 90'.
 */
@QueryExplorer(display = true)
pattern bentRightArm(RH:RightHand, // hand 
					 RE:RightElbow, // elbow
					 RS:RightShoulder // shoulder
) {
	// indicate coordinates to be used for check calculation
	PositionedElement.x(RH,RHX);
	PositionedElement.y(RH,RHY);
	PositionedElement.z(RH,RHZ);
	PositionedElement.x(RE,REX);
	PositionedElement.y(RE,REY);
	PositionedElement.z(RE,REZ);
	PositionedElement.x(RS,RSX);
	PositionedElement.y(RS,RSY);
	PositionedElement.z(RS,RSZ);
	// check that the angle between the lower arm (H-E)
	// and upper arm (S-E) is around PI/2 (90')
	check({
		// we need to "wire" attribute values into the check expression
		var float angleBetween = VectorMaths::angleBetween(
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("RHX").addValue(RHX).movingAvg, 
				MovingAverageCalculator::getCalculator("RHY").addValue(RHY).movingAvg, 
				MovingAverageCalculator::getCalculator("RHZ").addValue(RHZ).movingAvg, 
				MovingAverageCalculator::getCalculator("REX").addValue(REX).movingAvg, 
				MovingAverageCalculator::getCalculator("REY").addValue(REY).movingAvg, 
				MovingAverageCalculator::getCalculator("REZ").addValue(REZ).movingAvg
			),
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("RSX").addValue(RSX).movingAvg, 
				MovingAverageCalculator::getCalculator("RSY").addValue(RSY).movingAvg, 
				MovingAverageCalculator::getCalculator("RSZ").addValue(RSZ).movingAvg, 
				MovingAverageCalculator::getCalculator("REX").addValue(REX).movingAvg, 
				MovingAverageCalculator::getCalculator("REY").addValue(REY).movingAvg, 
				MovingAverageCalculator::getCalculator("REZ").addValue(REZ).movingAvg
			)
		)
		// System::out.println(angleBetween)
		Math::abs(Math::PI/2 - angleBetween) < YMCAConstants::angleTolerance
	});
}

/**
 * Left arm is stretched straight.
 */
@QueryExplorer(display = true)
pattern stretchedLeftArm(LH:LeftHand, // hand 
					 LE:LeftElbow, // elbow
					 LS:LeftShoulder // shoulder
) {
	// indicate coordinates to be used for check calculation
	PositionedElement.x(LH,LHX);
	PositionedElement.y(LH,LHY);
	PositionedElement.z(LH,LHZ);
	PositionedElement.x(LE,LEX);
	PositionedElement.y(LE,LEY);
	PositionedElement.z(LE,LEZ);
	PositionedElement.x(LS,LSX);
	PositionedElement.y(LS,LSY);
	PositionedElement.z(LS,LSZ);
	// check that the angle between the lower arm (H-E)
	// and upper arm (S-E) is around PI (180')
	check({
		// we need to "wire" attribute values into the check expression
		var float angleBetween = VectorMaths::angleBetween(
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("LHX").addValue(LHX).movingAvg, 
				MovingAverageCalculator::getCalculator("LHY").addValue(LHY).movingAvg, 
				MovingAverageCalculator::getCalculator("LHZ").addValue(LHZ).movingAvg, 
				MovingAverageCalculator::getCalculator("LEX").addValue(LEX).movingAvg, 
				MovingAverageCalculator::getCalculator("LEY").addValue(LEY).movingAvg, 
				MovingAverageCalculator::getCalculator("LEZ").addValue(LEZ).movingAvg
			),
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("LSX").addValue(LSX).movingAvg, 
				MovingAverageCalculator::getCalculator("LSY").addValue(LSY).movingAvg, 
				MovingAverageCalculator::getCalculator("LSZ").addValue(LSZ).movingAvg, 
				MovingAverageCalculator::getCalculator("LEX").addValue(LEX).movingAvg, 
				MovingAverageCalculator::getCalculator("LEY").addValue(LEY).movingAvg, 
				MovingAverageCalculator::getCalculator("LEZ").addValue(LEZ).movingAvg
			)
		)
		// System::out.println(angleBetween)
		(Math::PI - angleBetween) < YMCAConstants::angleTolerance
	});
}

/**
 * Left arm is bent 90'.
 */
@QueryExplorer(display = true)
pattern bentLeftArm(LH:LeftHand, // hand 
					LE:LeftElbow, // elbow
					LS:LeftShoulder // shoulder
) {
	// indicate coordinates to be used for check calculation
	PositionedElement.x(LH,LHX);
	PositionedElement.y(LH,LHY);
	PositionedElement.z(LH,LHZ);
	PositionedElement.x(LE,LEX);
	PositionedElement.y(LE,LEY);
	PositionedElement.z(LE,LEZ);
	PositionedElement.x(LS,LSX);
	PositionedElement.y(LS,LSY);
	PositionedElement.z(LS,LSZ);
	// check that the angle between the lower arm (H-E)
	// and upper arm (S-E) is around PI/2 (90')
	check({
		// we need to "wire" attribute values into the check expression
		var float angleBetween = VectorMaths::angleBetween(
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("LHX").addValue(LHX).movingAvg, 
				MovingAverageCalculator::getCalculator("LHY").addValue(LHY).movingAvg, 
				MovingAverageCalculator::getCalculator("LHZ").addValue(LHZ).movingAvg, 
				MovingAverageCalculator::getCalculator("LEX").addValue(LEX).movingAvg, 
				MovingAverageCalculator::getCalculator("LEY").addValue(LEY).movingAvg, 
				MovingAverageCalculator::getCalculator("LEZ").addValue(LEZ).movingAvg
			),
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("LSX").addValue(LSX).movingAvg, 
				MovingAverageCalculator::getCalculator("LSY").addValue(LSY).movingAvg, 
				MovingAverageCalculator::getCalculator("LSZ").addValue(LSZ).movingAvg, 
				MovingAverageCalculator::getCalculator("LEX").addValue(LEX).movingAvg, 
				MovingAverageCalculator::getCalculator("LEY").addValue(LEY).movingAvg, 
				MovingAverageCalculator::getCalculator("LEZ").addValue(LEZ).movingAvg
			)
		)
		// System::out.println(angleBetween)
		Math::abs(Math::PI/2 - angleBetween) < YMCAConstants::angleTolerance
	});
}


@QueryExplorer(display = true)
pattern upperArmsInY(LE:LeftElbow,
					 LS:LeftShoulder,
					 RE:RightElbow,
					 RS:RightShoulder
) {
	// indicate coordinates to be used for check calculation
	PositionedElement.x(LE,LEX);
	PositionedElement.y(LE,LEY);
	PositionedElement.z(LE,LEZ);
	PositionedElement.x(LS,LSX);
	PositionedElement.y(LS,LSY);
	PositionedElement.z(LS,LSZ);
	PositionedElement.x(RE,REX);
	PositionedElement.y(RE,REY);
	PositionedElement.z(RE,REZ);
	PositionedElement.x(RS,RSX);
	PositionedElement.y(RS,RSY);
	PositionedElement.z(RS,RSZ);
	// check that the angle between the left upper arm (LE-LS)
	// and right upper arm (RE-RS) is around PI/3 (60')
	check({
		// we need to "wire" attribute values into the check expression
		var float angleBetween = VectorMaths::angleBetween(
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("LEX").addValue(LEX).movingAvg, 
				MovingAverageCalculator::getCalculator("LEY").addValue(LEY).movingAvg, 
				MovingAverageCalculator::getCalculator("LEZ").addValue(LEZ).movingAvg,
				MovingAverageCalculator::getCalculator("LSX").addValue(LSX).movingAvg, 
				MovingAverageCalculator::getCalculator("LSY").addValue(LSY).movingAvg, 
				MovingAverageCalculator::getCalculator("LSZ").addValue(LSZ).movingAvg
			),
			VectorMaths::createVector(
				MovingAverageCalculator::getCalculator("REX").addValue(REX).movingAvg, 
				MovingAverageCalculator::getCalculator("REY").addValue(REY).movingAvg, 
				MovingAverageCalculator::getCalculator("REZ").addValue(REZ).movingAvg,
				MovingAverageCalculator::getCalculator("RSX").addValue(RSX).movingAvg, 
				MovingAverageCalculator::getCalculator("RSY").addValue(RSY).movingAvg, 
				MovingAverageCalculator::getCalculator("RSZ").addValue(RSZ).movingAvg
			)
		)
		// System::out.println(angleBetween)
		Math::abs(Math::PI/3 - angleBetween) < YMCAConstants::angleTolerance
	});
}

/**
 * The body is in the "Y" position.
 */
pattern Y(RH:RightHand, 
		  RE:RightElbow, 
		  RS:RightShoulder, 
		  LH:LeftHand, 
		  LE:LeftElbow, 
		  LS:LeftShoulder,
		  H:Head
) {
	// both arms are stretched
	find stretchedRightArm(RH,RE,RS);
	find stretchedLeftArm(LH,LE,LS);
	// both hands are above the head
	find rightHandAboveHead(RH,H);
	find leftHandAboveHead(LH,H);
	// angle between upper arms is around PI/3
	find upperArmsInY(LE,LS,RE,RS);
}

/**
 * The body is in the "I" position.
 */
pattern I(RH:RightHand, 
		  RE:RightElbow, 
		  RS:RightShoulder,
		  H:Head 
) {
	// both arms are stretched
	find stretchedRightArm(RH,RE,RS);
	find stretchedLeftArm(LH,LE,LS);
	// right hand above the head, left below
	find rightHandAboveHead(RH,H);
	neg find leftHandAboveHead(LH,H);
	// angle between upper arms is not around PI/3
	neg find upperArmsInY(LE,LS,RE,RS);
}

/**
 * The body is in the "A" position.
 */
pattern A(RH:RightHand, 
		  RE:RightElbow, 
		  RS:RightShoulder,
		  LH:LeftHand, 
		  LE:LeftElbow, 
		  LS:LeftShoulder,
		  H:Head
) {
	// both arms are bent
	find bentRightArm(RH,RE,RS);
	find bentLeftArm(LH,LE,LS);
	// both hands above the head and close
	find rightHandAboveHead(RH,H);
	find leftHandAboveHead(LH,H);
	// find handsAreClose(LH,RH);
	// angle between upper arms is around PI/3
	find upperArmsInY(LE,LS,RE,RS);
}

/**
 * The body is in the "M" position.
 */
pattern M(RH:RightHand, 
		  RE:RightElbow, 
		  RS:RightShoulder,
		  LH:LeftHand, 
		  LE:LeftElbow, 
		  LS:LeftShoulder,
		  H:Head
) {
	Head(H);
	// both arms are bent
	find bentRightArm(RH,RE,RS);
	find bentLeftArm(LH,LE,LS);
	// both hands below the head
	neg find rightHandAboveHead(RH,H);
	neg find leftHandAboveHead(LH,H);
	// elbows are around shoulder height
	LeftElbow.y(LE,LEY);
	LeftShoulder.y(LS,LSY);
	check(Math::abs(MovingAverageCalculator::getCalculator("LEY").addValue(LEY).movingAvg
		- MovingAverageCalculator::getCalculator("LSY").addValue(LSY).movingAvg) < YMCAConstants::veryCloseDistance);
	RightElbow.y(RE,REY);
	RightShoulder.y(RS,RSY);
	check(Math::abs(MovingAverageCalculator::getCalculator("REY").addValue(REY).movingAvg
		- MovingAverageCalculator::getCalculator("RSY").addValue(RSY).movingAvg) < YMCAConstants::veryCloseDistance);	
	// hands are apart
	find handsAreApart(LH,RH);
}

/**
 * The body is in the "Q" position.
 */
pattern Q(RH:RightHand, 
		  RE:RightElbow, 
		  RS:RightShoulder,
		  LH:LeftHand, 
		  LE:LeftElbow, 
		  LS:LeftShoulder,
		  LF:LeftFoot,
		  RF:RightFoot,
		  H:Head
) {
	// both arms are bent
	find bentRightArm(RH,RE,RS);
	find bentLeftArm(LH,LE,LS);
	// both hands below the head
	neg find rightHandAboveHead(RH,H);
	neg find leftHandAboveHead(LH,H);
	// hands are close
	find handsAreClose(LH,RH);
	// right leg is straight 
	// TODO
	// right foot is far away from left foot
	find feetAreApart(LF,RF);
}

/**
 * The body is in the "C" position.
 */
pattern C(RH:RightHand, 
		  RE:RightElbow, 
		  RS:RightShoulder,
		  LH:LeftHand, 
		  LE:LeftElbow, 
//		  LS:LeftShoulder,
		  H:Head
) {
	// both arms are bent
	find bentRightArm(RH,RE,RS);
	// find bentLeftArm(LH,LE,LS);
	// right hand below, left hand above the head
	neg find rightHandAboveHead(RH,H);
	neg find leftHandAboveHead(LH,H);
	// hands are not close
	// neg find handsAreClose(LH,RH);
	// left hand is to the right of the head
	Head.x(H,HX);
	LeftHand.x(LH,LHX);
	check(MovingAverageCalculator::getCalculator("LHX").addValue(LHX).movingAvg > 
		MovingAverageCalculator::getCalculator("HX").addValue(HX).movingAvg);
	// left elbow is to the right of the head
	LeftElbow.x(LE,LEX);
	check(MovingAverageCalculator::getCalculator("LEX").addValue(LEX).movingAvg > 
		MovingAverageCalculator::getCalculator("HX").addValue(HX).movingAvg);
		
}

// "control powerpoint with awt.Robot" demo

/*
 * Forward start position.
 */
pattern FS(RH: RightHand,
					  RE: RightElbow,
					  RS: RightShoulder,
					   H: Head) 
{
	find stretchedRightArm(RH,RE,RS);
	neg find rightHandAboveHead(RH,H);
}

/*
 * Forward end position.
 */
pattern FE(RH: RightHand,
					RE: RightElbow,
					RS: RightShoulder,
					 H: Head) 
{
	find bentRightArm(RH,RE,RS);
	neg find rightHandAboveHead(RH,H);
}

/*
 * Backward start position.
 */
pattern BS(LH: LeftHand,
					   LE: LeftElbow,
					   LS: LeftShoulder,
					   H: Head) 
{
	find stretchedLeftArm(LH,LE,LS);
	neg find leftHandAboveHead(LH,H);
}

/*
 * Backward end position.
 */
pattern BE(LH: LeftHand,
					 LE: LeftElbow,
					 LS: LeftShoulder,
					  H: Head) 
{
	find bentLeftArm(LH,LE,LS);
	neg find leftHandAboveHead(LH,H);
}


// control model train through Sheldon demo

/*
 * Sheldon gesture start position:
 *  - right hand around center hip height
 */
@QueryExplorer(display = true) 
pattern SS(RH: RightHand,
		   //RE: RightElbow,
		   //RS: RightShoulder,
		   CH : CenterHip)
{
	//find stretchedRightArm(RH,RE,RS);
	RightHand.y(RH,RHy);
	CenterHip.y(CH,CHy);
	check(
		Math::abs(
		 MovingAverageCalculator::getCalculator("CHY").addValue(CHy).movingAvg - 
		 MovingAverageCalculator::getCalculator("RHY").addValue(RHy).movingAvg) 
		 < YMCAConstants::closeDistance
	);				   	
}

/*
 * Sheldon gesture middle position:
 *  - stretched right arm
 *  - right hand around shoulder's height
 */
@QueryExplorer(display = true) 
pattern SM(RH: RightHand,
		   RE: RightElbow,
		   RS: RightShoulder
		   //H : Head
		   )
{
	find stretchedRightArm(RH,RE,RS);
	RightHand.y(RH,RHy);
	RightShoulder.y(RS,RSy);
	check(
		Math::abs(
		 MovingAverageCalculator::getCalculator("RSY").addValue(RSy).movingAvg - 
		 MovingAverageCalculator::getCalculator("RHY").addValue(RHy).movingAvg) 
		 < YMCAConstants::closeDistance
	);		   	
}

/*
 * Sheldon gesture end position:
 *  - stretched right arm
 *  - right hand significantly above head's height
 */
@QueryExplorer(display = true) 
pattern SE(RH: RightHand,
		   RE: RightElbow,
		   RS: RightShoulder,
		   H : Head)
{
	find stretchedRightArm(RH,RE,RS);
	RightHand.y(RH,RHy);
	Head.y(H,Hy);
	check(
		 MovingAverageCalculator::getCalculator("RHY").addValue(RHy).movingAvg -
		 MovingAverageCalculator::getCalculator("HY").addValue(Hy).movingAvg
		 > YMCAConstants::veryCloseDistance
	);		   	
}
 